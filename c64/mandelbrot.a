

MAX_ITERATIONS = 14

mandelbrot_fp_4:      +fp  4.0
mandelbrot_fp_0:      +fp  0.0
mandelbrot_fp_dC:     +fp  0.01
mandelbrot_fp_c01:    +fp -2.0
mandelbrot_fp_c1:     +fp  0.0
mandelbrot_fp_c2:     +fp -1.0

drawMandelbrot:

    jsr fpinit
    jsr initGraphics

    lda #0
    sta graphics_y
    sta graphics_y + 1
    
.forY:
    +fpmov mandelbrot_fp_c1, mandelbrot_fp_c01
    lda #0
    sta graphics_x
    sta graphics_x + 1

.forX:
    +fpmov mandelbrot_fp_z1, mandelbrot_fp_0
    +fpmov mandelbrot_fp_z2, mandelbrot_fp_0
    +fpmov mandelbrot_fp_z1Sq, mandelbrot_fp_0
    +fpmov mandelbrot_fp_z2Sq, mandelbrot_fp_0

    lda #MAX_ITERATIONS
    sta mandelbrot_i

.forI: 
    jsr updateZ
    bpl .nextX
    ldx mandelbrot_i
    dex
    stx mandelbrot_i
    bne .forI

    ; set x,y and also x, (200 - y) because of pattern symmetry
    jsr setPoint
    lda #172
    sbc graphics_y
    sta graphics_y

    jsr setPoint
    lda #172
    sbc graphics_y
    sta graphics_y

.nextX:
    ldx graphics_x
    inx 
    stx graphics_x
    beq .nextY

    +fpadd mandelbrot_fp_c1, mandelbrot_fp_dC

    jmp .forX

.nextY:
    ldx graphics_y
    inx 
    stx graphics_y
    beq .done

    +fpadd mandelbrot_fp_c2, mandelbrot_fp_dC

    jmp .forY
.done:
    rts

; compute z = z^2 + c, update z^2
; N is set if |z| > 4
updateZ:
    ; tmp = z1 * z1 - z2 * z2 + c1 
    
    +fpmov mandelbrot_fp_tmp, mandelbrot_fp_c1
    +fpadd mandelbrot_fp_tmp, mandelbrot_fp_z1Sq
    +fpsub mandelbrot_fp_tmp, mandelbrot_fp_z2Sq

    ; now compute z2 = 2 * z1 * z2 + c2
    +fpmul mandelbrot_fp_z2, mandelbrot_fp_z1
    +fpadd mandelbrot_fp_z2, mandelbrot_fp_z2 ;*2
    +fpadd mandelbrot_fp_z2, mandelbrot_fp_c2

    ; z1 = tmp
    +fpmov mandelbrot_fp_z1, mandelbrot_fp_tmp

    ; update z1^2 and z2^2
    +fpmov mandelbrot_fp_z1Sq, mandelbrot_fp_z1
    +fpmul mandelbrot_fp_z1Sq, mandelbrot_fp_z1Sq
    +fpmov mandelbrot_fp_z2Sq, mandelbrot_fp_z2
    +fpmul mandelbrot_fp_z2Sq, mandelbrot_fp_z2Sq

    ; fac1 = z1 * z1 + z2 * z2
    +fpmov mandelbrot_fp_tmp, mandelbrot_fp_z1Sq
    +fpadd mandelbrot_fp_tmp, mandelbrot_fp_z2Sq
    +fpsub mandelbrot_fp_tmp, mandelbrot_fp_4
    rts
